buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.gradle.publish:plugin-publish-plugin:0.9.9'
    }
}

plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
}

apply plugin: 'com.gradle.plugin-publish'

group 'de.dfki.mary'
version '0.1-SNAPSHOT'
description 'Crossvalidation for voicebuilding with MaryTTS'

ext.pluginId = 'de.dfki.mary.voicebuilding.marytts-crossvalidation'

gradlePlugin {
    plugins {
        maryttsCVPlugin {
            id = pluginId
            implementationClass = 'de.dfki.mary.voicebuilding.MaryttsCrossvalidationPlugin'
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    compile localGroovy()
    compile group: 'de.dfki.mary', name: 'marytts-runtime', version: '5.2', {
        exclude module: 'groovy-all'
    }
    testCompile 'org.testng:testng:6.9.6'
}

test {
    useTestNG()
    testLogging {
        exceptionFormat = 'full'
    }
}


task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

pluginBundle {
    def url = 'https://github.com/marytts/gradle-marytts-crossvalidation-plugin'
    website = url
    vcsUrl = url
    description = project.description
    tags = ['tts', 'speech synthesis', 'evaluation', 'marytts']

    plugins {
        maryttsCVPlugin {
            id = pluginId
            displayName = project.description
        }
    }
}
